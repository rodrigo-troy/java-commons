plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'com.rodrigotroy'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.AMAZON
    }

    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }
}


repositories {
    mavenLocal()

    mavenCentral()
}

dependencies {
    api 'org.jetbrains:annotations:26.0.1'

    implementation platform('org.apache.logging.log4j:log4j-bom:2.24.3')
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'

    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.testng:testng:7.8.0'

}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(17)
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs = ["-Xlint:unchecked", "-Xlint:deprecation", "-parameters"]
}

tasks.jar {
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(rootProject.version.toString())

    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': rootProject.version)
    }
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
            it.candidate.version.toLowerCase().contains(qualifier)
        }
    }
}

tasks.register("checkDependencyUpdates") {
    group = "My Custom Tasks"
    description = "Checks for available updates of all dependencies."
    dependsOn 'dependencyUpdates'
}
